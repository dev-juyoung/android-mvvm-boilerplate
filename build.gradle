// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'config.gradle'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.gradle.plugin
        classpath deps.kotlin.plugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    configurations {
        ktlint
    }

    dependencies {
        ktlint deps.devtools.ktlint
    }

    // 스타일을 검사하는 태스크
    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "src/**/*.kt"
    }

    // 스타일을 검사하고 맞지 않는 부분을 수정해주는 태스크
    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.github.shyiko.ktlint.Main"
        args "-F", "src/**/*.kt"
    }

    // 'check' 태스크가 실행될 때 'ktlint' 태스크도 함께 수행되도록 지정
    afterEvaluate { project ->
        check.dependsOn ktlint
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
